--
-- Binding between FlowDiagram concept and BPMN
-- 
-- Some links about BPMN:
--   * http://tynerblain.com/blog/2006/07/27/bpmn-gateways/
binding BPMN for FD { 
	class FlowDiagram to BpmnDiagram	
	class Node        to Vertex
	class FlowEdge    to SequenceEdge
	
	-- So far, only 1:1 mapping
	class Task            to Activity when self.activityType = #Task              or
	                                       self.activityType = #EventStartEmpty   or
	     						           self.activityType = #EventEndEmpty     or 
	     						           self.activityType = #EventEndTerminate
	     						           
	class ExclusiveChoice to Activity when self.activityType = #GatewayDataBasedExclusive or 
	                                       self.activityType = #GatewayEventBasedExclusive
	                                        
	class ParallelSplit   to Activity when self.activityType = #GatewayParallel and
	                                       self.incomingEdges.size() = 1        and 
	                                       self.outgoingEdges.size() > 1

	class Synchronization to Activity when self.activityType = #GatewayParallel and
	                                       self.outgoingEdges.size() = 1        and
	                                       self.incomingEdges.size() > 1

	class MultiChoice     to Activity when self.activityType = #GatewayDataBasedInclusive and
	                                       self.outgoingEdges.size() > 1        and
	                                       self.incomingEdges.size() = 1

	class SimpleMerge     to Activity when self.activityType = #GatewayDataBasedInclusive and
	                                       self.outgoingEdges.size() = 1        and
	                                       self.incomingEdges.size() > 1
	
	feature FlowDiagram.nodes = self.pools->collect(p | p.vertices).flatten()
	feature FlowDiagram.edges = self.pools->collect(p | p.sequenceEdges).flatten()

	feature FlowEdge."in" is source
	feature FlowEdge.out is target

	feature Node."in" is incomingEdges
	feature Node.out is outgoingEdges


	feature Task.isInitial = self.activityType = #EventStartEmpty
	feature Task.isFinal   = self.activityType = #EventEndEmpty or self.activityType = #EventEndTerminate    
}


